plugins {
    id "org.beryx.jlink" version "3.0.1"
    id "com.diffplug.spotless" version "6.25.0"
    id "org.asciidoctor.jvm.convert" version "4.0.3"
}

dependencies {
    implementation(libs.codion.swing.framework.ui)

    implementation(libs.json)
    implementation(libs.flatlaf.intellij.themes)

    runtimeOnly(libs.codion.plugin.logback.proxy)

    runtimeOnly(libs.codion.framework.db.local)
    runtimeOnly(libs.codion.dbms.h2)
    runtimeOnly(libs.h2)

    testImplementation(libs.codion.framework.domain.test)
    testImplementation(libs.junit.api)
    testRuntimeOnly(libs.junit.engine)
}

version = libs.versions.codion.get().replace("-SNAPSHOT", "")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

spotless {
    java {
        licenseHeaderFile("${rootDir}/license_header").yearSeparator(' - ')
    }
    format 'javaMisc', {
        target 'src/**/package-info.java', 'src/**/module-info.java'
        licenseHeaderFile("${rootDir}/license_header", '\\/\\*\\*').yearSeparator(' - ')
    }
}

test {
    useJUnitPlatform()
    systemProperty "codion.db.url", "jdbc:h2:mem:h2db"
    systemProperty "codion.db.initScripts", "classpath:create_schema.sql"
    systemProperty "codion.test.user", "scott:tiger"
}

applicationDefaultJvmArgs = [
        "-Xmx64m",
        "-Dcodion.client.connectionType=local",
        "-Dcodion.db.url=jdbc:h2:mem:h2db",
        "-Dcodion.db.initScripts=classpath:create_schema.sql",
        "-Dsun.awt.disablegrab=true"
]

application {
    mainModule.set("is.codion.framework.demos.petclinic")
    mainClass.set("is.codion.framework.demos.petclinic.ui.PetclinicAppPanel")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.deprecation = true
}

asciidoctor {
    inputs.dir("src")
    baseDirFollowsSourceFile()
    attributes "codion-version": project.version
    asciidoctorj {
        version = "2.5.13"
        attributes "source-highlighter": "prettify", tabsize: 2
    }
}

tasks.register("writeVersion", WriteProperties) {
    destinationFile = file(temporaryDir.getAbsolutePath() + "/version.properties")

    property "version", libs.versions.codion.get().replace("-SNAPSHOT", "")
}

processResources {
    from(writeVersion)
}

tasks.register("copyToGitHubPages", Sync) {
    dependsOn asciidoctor
    group "documentation"
    def documentationDir = project.version
    from project.layout.buildDirectory.dir("docs/asciidoc")
    into "../codion-pages/doc/${documentationDir}/tutorials/petclinic"
}

jlink {
    imageName.set(project.name)
    moduleName.set(application.mainModule)
    options = ["--strip-debug", "--no-header-files", "--no-man-pages", "--add-modules",
               "is.codion.framework.db.local,is.codion.dbms.h2,is.codion.plugin.logback.proxy"]

    addExtraDependencies("slf4j-api")

    launcher {
        jvmArgs = applicationDefaultJvmArgs
    }

    jpackage {
        imageName = "Petclinic"
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = "deb"
            icon = "src/main/icons/petclinic.png"
            installerOptions = [
                    "--resource-dir", "build/jpackage/Petclinic/lib",
                    "--linux-shortcut"
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = "msi"
            icon = "src/main/icons/petclinic.ico"
            installerOptions = [
                    "--win-menu",
                    "--win-shortcut"
            ]
        }
    }
}